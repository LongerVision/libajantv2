cmake_minimum_required(VERSION 3.10)

# For CMAKE_MSVC_RUNTIME_LIBRARY compatability. Per note: This must be before the first project()
if(POLICY CMP0091)
  cmake_policy(SET CMP0091 NEW)
endif()

project(libajantv2)

if (NOT CMAKE_MODULE_PATH)
    set(CMAKE_MODULE_PATH "${CMAKE_CURRENT_SOURCE_DIR}/cmake/")
endif()

include(CMakeOptions)
include(CompilerFlags)
include(WindowsHelpers)
include(ntv2/Defines)
include(ntv2/Helpers)
include(ntv2/Version)

if (CMAKE_SYSTEM_NAME STREQUAL "Windows")
	if (DEFINED AJA_MSVC_RUNTIME_LIBRARY)
		aja_set_global_msvc_runtime_library(${AJA_MSVC_RUNTIME_LIBRARY})
	endif()
endif()

option(AJANTV2_BUILD_OPENSOURCE "Build libajantv2 as open-source (MIT license)?" ON)
option(AJANTV2_BUILD_SHARED     "Build libajantv2 shared libraries?" OFF)
option(AJANTV2_BUILD_DEMOS      "Build libajantv2 demo apps?" ON)
option(AJANTV2_BUILD_TOOLS      "Build libajantv2 tools?" ON)
option(AJANTV2_BUILD_TESTS      "Build libajantv2 tests?" ON)
option(AJANTV2_BUILD_DOCS       "Build libajantv2 docs?" OFF)

if(NOT AJANTV2_BUILD_OPENSOURCE)
    set(NTV2_PROPRIETARY_DIR ${CMAKE_SOURCE_DIR}/proprietary)
endif()

add_subdirectory(ajantv2)
if (AJANTV2_BUILD_DEMOS)
    add_subdirectory(demos)
endif()

if (AJANTV2_BUILD_TOOLS)
    add_subdirectory(tools)
endif()

if (AJANTV2_BUILD_TESTS)
    include(CTest)
    if (NOT TARGET doctest::doctest)
        set (DOCTEST_PATH ${CMAKE_SOURCE_DIR}/thirdparty/doctest)
        add_subdirectory(${DOCTEST_PATH})
        include(${DOCTEST_PATH}/scripts/cmake/doctest.cmake)
    endif()
    add_subdirectory(ajantv2/test)
endif()
