function(aja_ntv2_log_build_info)
	string(TIMESTAMP _build_time "%A, %B %d, %Y at %H:%M:%S GMT-8")
	aja_message(STATUS "---\n\nAJA NTV2 SDK ${AJA_NTV2_VER_STR} ${AJA_NTV2_SDK_BUILD_TYPE}${AJA_NTV2_SDK_BUILD_NUMBER}\nBuilt on: ${_build_time}\n")
	if (AJA_GIT_COMMIT_HASH_SHORT)
		aja_message(STATUS "Built from commit: ${AJA_GIT_COMMIT_HASH_SHORT}")
	endif()
	if (AJA_NTV2_BUILD_OPENSOURCE)
		aja_message(STATUS "Building open-source AJA NTV2 SDK and apps")
	else()
		aja_message(STATUS "Building proprietary AJA NTV2 SDK and apps")
	endif()
	aja_message(STATUS "Platform: ${CMAKE_SYSTEM_NAME}")
	aja_message(STATUS "Arch: ${CMAKE_HOST_SYSTEM_PROCESSOR}")
	aja_message(STATUS "Bits: ${AJA_BITS}")
	aja_message(STATUS "Compiler: ${CMAKE_CXX_COMPILER_ID}")
	aja_message(STATUS "C++${CMAKE_CXX_STANDARD} C${CMAKE_C_STANDARD}")
	aja_message(STATUS "Install Prefix: ${CMAKE_INSTALL_PREFIX}")
	if (CMAKE_SYSTEM_NAME STREQUAL "Windows")
		aja_message(STATUS "Windows API Version: ${CMAKE_SYSTEM_VERSION}")
		string(REPLACE "." ";" AJA_WIN_API_VER "${CMAKE_SYSTEM_VERSION}")
		list(GET AJA_WIN_API_VER 0 AJA_WIN_API_VER_MAJOR)
		list(GET AJA_WIN_API_VER 1 AJA_WIN_API_VER_MINOR)
		list(GET AJA_WIN_API_VER 2 AJA_WIN_API_VER_BUILD)
		aja_message(STATUS "MSVC Version: ${MSVC_VERSION}")
		aja_get_msvc_tools_version()
		aja_message(STATUS "MSVC_TOOLSET: v${AJA_MSVC_TOOLSET_VERSION}")
	elseif(CMAKE_SYSTEM_NAME STREQUAL "Darwin")
		aja_message(STATUS "macOS Architectures: ${CMAKE_OSX_ARCHITECTURES}")
		aja_message(STATUS "macOS Deployment Target: ${CMAKE_OSX_DEPLOYMENT_TARGET}")
	elseif(CMAKE_SYSTEM_NAME STREQUAL "Linux")
	else()
		aja_message(FATAL_ERROR "Unsupported platform: ${CMAKE_SYSTEM_NAME}")
	endif()
	aja_message(STATUS "Python 2: ${Python2_EXECUTABLE}")
	aja_message(STATUS "Python 3: ${Python3_EXECUTABLE}")
	aja_message(STATUS "Git: ${GIT_EXECUTABLE}")
endfunction(aja_ntv2_log_build_info)

function(aja_ntv2_sdk_gen target_deps)
	add_custom_command(
		TARGET ${target_deps} PRE_BUILD
		COMMAND
			${Python2_EXECUTABLE} ${AJA_NTV2_ROOT}/ajalibraries/ajantv2/sdkgen/ntv2sdkgen.py
			--verbose --unused --ajantv2 ajalibraries/ajantv2 --ohh ajalibraries/ajantv2/includes --ohpp ajalibraries/ajantv2/src
		WORKING_DIRECTORY ${AJA_NTV2_ROOT}
		COMMENT "Running ntv2sdkgen script...")
endfunction(aja_ntv2_sdk_gen)
